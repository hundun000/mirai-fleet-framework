buildscript {
    ext {
        
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'net.mamoe.mirai-console' version "${MIRAI_VERSION}"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
    apply plugin: 'net.mamoe.mirai-console'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'java-library'
    
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
//    test {
//        useJUnitPlatform()
//    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    artifacts {
        archives sourcesJar
    }
}




project(":framework-core") {
    apply plugin: 'maven-publish'
    
    eclipse.project.name = rootProject.name + "-core"

    dependencies {
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.4'
    
        compileOnly 'org.projectlombok:lombok:1.18.4'
        annotationProcessor 'org.projectlombok:lombok:1.18.4'
    }
    
    publishing {
        publications {
            mavenJava(MavenPublication) {
                // useless for jitpack: groupId = GROUP
                artifactId = ARTIFACT_ID_START + "-core"
                // useless for jitpack: version = VERSION
    
                from components.java
            }
        }
    }
}


project(":framework-starter") {
    apply plugin: 'maven-publish'

    eclipse.project.name = rootProject.name + "-starter"

    dependencies {
        implementation project(":framework-core")
    
        api 'io.github.openfeign:feign-jackson:11.1'
        api 'io.github.openfeign:feign-core:11.1'
        
        api 'org.quartz-scheduler:quartz:2.3.2'
    
        compileOnly 'org.projectlombok:lombok:1.18.4'
        annotationProcessor 'org.projectlombok:lombok:1.18.4'
    }
    
    publishing {
        publications {
            mavenJava(MavenPublication) {
                // useless for jitpack: groupId = GROUP
                artifactId = ARTIFACT_ID_START + "-starter"
                // useless for jitpack: version = VERSION
    
                from components.java
            }
        }
    }
}

project(":framework-example") {
    // useless for builPlugin: group = GROUP
    version = BUILD_PLUGIN_VERSION
    
    eclipse.project.name = rootProject.name + "-example"    

    dependencies {
        implementation project(":framework-core")
        implementation project(":framework-starter")
        //implementation 'com.github.hundun000.mirai-fleet-framework:mirai-fleet-framework-core:0.5.0'
        //implementation 'com.github.hundun000.mirai-fleet-framework:mirai-fleet-framework-starter:0.5.0'
        
        compileOnly 'org.projectlombok:lombok:1.18.4'
        annotationProcessor 'org.projectlombok:lombok:1.18.4'
        
        testImplementation "junit:junit:4.11"
    }
}





